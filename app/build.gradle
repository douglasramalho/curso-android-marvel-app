apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-parcelize'
apply from: '../config/detekt/detekt.gradle'

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            storeFile file('/Users/Walcker/ANDROIDSTUDIO/Vitor/curso-android-marvel-app/store/app_key_marvel_store')
            storePassword 'walcker178977'
            keyAlias 'marvel_app'
            keyPassword 'walcker178977'
        }
    }

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner rootProject.ext.customTestRunner
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY'] 
        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    buildTypes {
        debug{
            applicationIdSuffix '.debug'
            debuggable = true
        }
        staging{
            initWith debug
            minifyEnabled true
            applicationIdSuffix '.staging'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.javaVersion
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    def presentationDependencies = rootProject.ext.presentationDependencies
    def masterTestDependencies = rootProject.ext.masterTestDependencies
    def masterDependencies = rootProject.ext.masterDependencies
    def dataDependencies = rootProject.ext.dataDependencies

    implementation project(rootProject.ext.modules.core)

    //Android
    implementation masterDependencies.constraintlayout
    implementation masterDependencies.androidCoreKtx
    implementation masterDependencies.legacySupport
    implementation masterDependencies.appcompat
    implementation masterDependencies.coroutines_android

    kapt(dataDependencies.roomCompiler) {
        exclude group: 'com.intellij', module: 'annotations'
    }
    implementation dataDependencies.roomRuntime
    implementation dataDependencies.roomKtx

    // Libs
    implementation presentationDependencies.lifecycleViewmodel
    implementation presentationDependencies.navigationFragment
    implementation presentationDependencies.lifecycleLivedata
    implementation presentationDependencies.lifecycleRuntime
    implementation presentationDependencies.materialVersion
    implementation presentationDependencies.navigationUi
    implementation presentationDependencies.glideGlide
    implementation presentationDependencies.datastore
    implementation presentationDependencies.shimmer
    kapt presentationDependencies.glideCompiler

    implementation "com.google.firebase:firebase-config:21.1.0"

    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Tests
    androidTestImplementation presentationTestDependencies.espressoContrib
    androidTestImplementation presentationTestDependencies.mockWebServer
    androidTestImplementation presentationTestDependencies.espressoCore
    debugImplementation presentationTestDependencies.fragmentTest
    androidTestUtil presentationTestDependencies.orchestrator
    testImplementation project(":testing")

    androidTestImplementation masterTestDependencies.junitTestVersion
    androidTestImplementation masterTestDependencies.runnerTest
    implementation masterTestDependencies.roomTesting

    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation masterTestDependencies.kotlinxCoroutinesTest
}